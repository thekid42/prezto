#
# A theme based on the 'redhat' theme included in a default zsh install and the 'steeef' theme included with prezto
#

function prompt_thekid_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="%F{cyan}(%F{magenta}%s:%F{cyan}%b%f%u%c%F{red}●%F{cyan})"
  else
    branch_format="%F{cyan}(%F{magenta}%s:%F{cyan}%b%f%u%c%F{cyan})"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_thekid_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_thekid_precmd

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(%F{cyan}%b%f%u%c)"
  local action_format="(%F{green}%a%f)"
  local unstaged_format="%F{yellow}●%f"
  local staged_format="%F{green}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Define prompts.
  PROMPT="%B%F{cyan}[%F{yellow}%n%F{blue}@%F{cyan}%m %F{blue}%~%f"'${vcs_info_msg_0_}'"%F{cyan}]%(?.%F{green}.%F{red})%(#.#.$)%f%b "
  RPROMPT=''
}

prompt_thekid_setup "$@"

